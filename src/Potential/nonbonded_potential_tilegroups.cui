// -*-c++-*-
#include "copyright.h"

#define EXCL_GMEM_OFFSET  (blockIdx.x * gmem_r.max_atoms)

/// \brief Compute the non-bonded energy and forces due to electrostatic, van-der Waals, and
///        derivatives of Generalized Born radii.
///
/// \param poly_nbk  Condensed non-bonded parameter tables and atomic properties for all systems
/// \param poly_se   Static exclusion masks spanning all systems
/// \param ctrl      Molecular mechanics control data
/// \param poly_psw  Coordinates and forces for all particles
/// \param scw       Energy tracking object
/// \param gmem_r    Workspaces for each thread block
__global__ void __launch_bounds__(small_block_size, NONBOND_KERNEL_BLOCKS_MULTIPLIER)
KERNEL_NAME(const SyNonbondedKit<TCALC, TCALC2> poly_nbk, const SeMaskSynthesisReader poly_se,
            const MMControlKit<TCALC> ctrl, PsSynthesisWriter poly_psw,
#ifdef COMPUTE_ENERGY
            ScoreCardWriter scw,
#endif
            CacheResourceKit<TCALC> gmem_r) {

  // Coordinates and properties of particles are copied into special, L1-cached arrays of GMEM
  // used exclusively by this block.
  __shared__ TCALC sh_tile_xcog[small_block_max_imports];
  __shared__ TCALC sh_tile_ycog[small_block_max_imports];
  __shared__ TCALC sh_tile_zcog[small_block_max_imports];
  __shared__ TCALC sh_tile_tpts[small_block_max_imports];
#ifdef COMPUTE_ENERGY
  __shared__ int sh_system_indices[small_block_max_imports];
#endif
  __shared__ int sh_n_lj_types[small_block_max_imports];
  __shared__ int sh_ljabc_offsets[small_block_max_imports];
#ifdef COMPUTE_FORCE
#  ifdef SPLIT_FORCE_ACCUMULATION
#    ifdef TCALC_IS_SINGLE
  __shared__ int sh_xfrc[small_block_max_atoms];
  __shared__ int sh_yfrc[small_block_max_atoms];
  __shared__ int sh_zfrc[small_block_max_atoms];
#    else
  __shared__ llint sh_xfrc[small_block_max_atoms];
  __shared__ llint sh_yfrc[small_block_max_atoms];
  __shared__ llint sh_zfrc[small_block_max_atoms];
#    endif
  __shared__ int sh_xfrc_overflow[small_block_max_atoms];
  __shared__ int sh_yfrc_overflow[small_block_max_atoms];
  __shared__ int sh_zfrc_overflow[small_block_max_atoms];
#  else
  __shared__ llint sh_xfrc[small_block_max_atoms];
  __shared__ llint sh_yfrc[small_block_max_atoms];
  __shared__ llint sh_zfrc[small_block_max_atoms]; 
#  endif
#endif
#ifdef COMPUTE_ENERGY
  __shared__ llint sh_elec_acc[small_block_max_imports];
  __shared__ llint sh_vdw_acc[small_block_max_imports];
  __shared__ llint sh_gb_acc[small_block_max_imports];
#endif
  __shared__ int nbwu_map[tile_groups_wu_abstract_length];
  __shared__ int nbwu_idx;

  // Each block takes its first non-bonded work unit based on its block index.
  if (threadIdx.x == 0) {
    nbwu_idx = blockIdx.x;
  }
  __syncthreads();
  while (nbwu_idx < poly_nbk.nnbwu) {

    // The instruction set is read and stored in __shared__ for convenience.  With a minimum of
    // 256 threads per block, there will be at least four warps, even on loewer-tier AMD
    // architectures.  This will be sufficient to do all of the initializations below without
    // an extra loop.
    const int padded_abstract_length = devcRoundUp(tile_groups_wu_abstract_length, warp_size_int);
    if (threadIdx.x < tile_groups_wu_abstract_length) {
      nbwu_map[threadIdx.x] =__ldcv(&poly_nbk.nbwu_abstracts[(nbwu_idx *
                                                              tile_groups_wu_abstract_length) +
                                                             threadIdx.x]);
    }
#ifdef COMPUTE_ENERGY
    else if (threadIdx.x >= padded_abstract_length &&
             threadIdx.x <  padded_abstract_length + small_block_max_imports) {
      sh_elec_acc[threadIdx.x - padded_abstract_length] = 0LL;
      sh_vdw_acc[ threadIdx.x - padded_abstract_length] = 0LL;
      sh_gb_acc[  threadIdx.x - padded_abstract_length] = 0LL;
    }
#endif
    __syncthreads();
    
    // Import atomic coordinates and properties.  Each warp will handle importing one of the
    // Cartesian coordinates or properties of as many tile sides as it can handle, in order to
    // get the most threads reaching out to global memory.
    const int tile_sides_per_warp = (warp_size_int / tile_length);
    const int warp_idx = (threadIdx.x >> warp_bits);
    const int warp_lane_idx = (threadIdx.x & warp_bits_mask_int);
    const int tile_side_offset = warp_lane_idx / tile_length;
    const int import_count = nbwu_map[0];
    int pos = (tile_sides_per_warp * warp_idx) + tile_side_offset;
#ifdef TCALC_IS_SINGLE
    pos = loadTileCoordinates(pos, 0, nbwu_map, poly_psw.xcrd, &gmem_r.xcrd[EXCL_GMEM_OFFSET],
                              sh_tile_xcog, poly_psw.gpos_scale_f);
    pos = loadTileCoordinates(pos, 1, nbwu_map, poly_psw.ycrd, &gmem_r.ycrd[EXCL_GMEM_OFFSET],
                              sh_tile_ycog, poly_psw.gpos_scale_f);
    pos = loadTileCoordinates(pos, 2, nbwu_map, poly_psw.zcrd, &gmem_r.zcrd[EXCL_GMEM_OFFSET],
                              sh_tile_zcog, poly_psw.gpos_scale_f);
#else
    pos = loadTileCoordinates(pos, 0, nbwu_map, poly_psw.xcrd, &gmem_r.xcrd[EXCL_GMEM_OFFSET],
                              poly_psw.xcrd_ovrf, &gmem_r.xcrd_ovrf[EXCL_GMEM_OFFSET],
                              sh_tile_xcog, poly_psw.gpos_scale);
    pos = loadTileCoordinates(pos, 1, nbwu_map, poly_psw.ycrd, &gmem_r.ycrd[EXCL_GMEM_OFFSET],
                              poly_psw.ycrd_ovrf, &gmem_r.ycrd_ovrf[EXCL_GMEM_OFFSET],
                              sh_tile_ycog, poly_psw.gpos_scale);
    pos = loadTileCoordinates(pos, 2, nbwu_map, poly_psw.zcrd, &gmem_r.zcrd[EXCL_GMEM_OFFSET],
                              poly_psw.zcrd_ovrf, &gmem_r.zcrd_ovrf[EXCL_GMEM_OFFSET],
                              sh_tile_zcog, poly_psw.gpos_scale);
#endif
    pos = loadTileProperty(pos, 3, nbwu_map, poly_nbk.charge, &gmem_r.charges[EXCL_GMEM_OFFSET],
                           SQRT_FUNC(poly_nbk.coulomb));
    pos = loadTileProperty(pos, 4, nbwu_map, poly_nbk.lj_idx, &gmem_r.lj_idx[EXCL_GMEM_OFFSET]);
    const int padded_import_count = devcRoundUp(import_count, tile_sides_per_warp);
    const int warps_per_block = blockDim.x >> warp_bits;
    const int tile_lane_idx = (threadIdx.x & tile_length_bits_mask);
    while (pos < 6 * padded_import_count) {
      const int rel_pos = pos - (5 * padded_import_count);
      if (rel_pos < import_count) {
        if (tile_lane_idx == 0) {

          // Stuff the total number of particles in this tile side into its shared memory slot.
          // Obtain this system's Lennard-Jone atom type count.  Transfer the system index of this
          // group of atoms (this "tile side") into the appropriate __shared__ memory slot.
          // Obtain the Lennard-Jones table offsets for this tile's system.
          sh_tile_tpts[rel_pos] = (TCALC)(getTileSideAtomCount(nbwu_map, rel_pos));
          const size_t system_idx = nbwu_map[small_block_max_imports + 8 + rel_pos];
          sh_n_lj_types[rel_pos] = poly_nbk.n_lj_types[system_idx];
#ifdef COMPUTE_ENERGY
          sh_system_indices[rel_pos] = system_idx;
#endif
          sh_ljabc_offsets[rel_pos] = poly_nbk.ljabc_offsets[system_idx];
        }          
      }
      pos += tile_sides_per_warp * warps_per_block;
    }
#ifdef COMPUTE_FORCE
    while (pos < 7 * padded_import_count) {
      const int rel_pos = pos - (6 * padded_import_count);
      if (rel_pos < import_count) {
        const size_t write_idx = (rel_pos * tile_length) + tile_lane_idx;
#  ifdef SPLIT_FORCE_ACCUMULATION
#    ifdef TCALC_IS_SINGLE
        sh_xfrc[write_idx] = 0;
        sh_yfrc[write_idx] = 0;
        sh_zfrc[write_idx] = 0;
#    else
        sh_xfrc[write_idx] = 0LL;
        sh_yfrc[write_idx] = 0LL;
        sh_zfrc[write_idx] = 0LL;
#    endif
        sh_xfrc_overflow[write_idx] = 0;
        sh_yfrc_overflow[write_idx] = 0;
        sh_zfrc_overflow[write_idx] = 0;
#  else
        sh_xfrc[write_idx] = 0LL;
        sh_yfrc[write_idx] = 0LL;
        sh_zfrc[write_idx] = 0LL;
#  endif
      }
      pos += tile_sides_per_warp * warps_per_block;
    }
#endif
    __syncthreads();

    // Loop over tile instructions
    pos = nbwu_map[small_block_max_imports + 6] + warp_idx;
    while (pos < nbwu_map[small_block_max_imports + 7]) {

      // The following code will handle one tile per warp.  On NVIDIA architectures and AMD
      // architectures with 32 threads per warp, this means that the first and second halves of
      // the warp handle the abscissa and ordinate atoms, respectively.  On AMD architectures
      // with 64 threads per warp, the atom data could be replicated with slight tweaks to the
      // shuffling pattern.  On Intel architectures with 16 threads per warp, each thread will
      // need to store both abscissa and ordinate atoms.
      uint2 tinsr = poly_nbk.nbwu_insr[pos];
      const uint t_mask = poly_se.mask_data[tinsr.y + warp_lane_idx];
      const int local_absc_start = (tinsr.x & 0xffff);
      const int local_ordi_start = ((tinsr.x >> 16) & 0xffff);
      const int absc_import_idx = local_absc_start >> tile_length_bits;
      const int ordi_import_idx = local_ordi_start >> tile_length_bits;

      // Obtain the centering for best single-precision results.  This can be done in either
      // precision level.  It is unnecessary with double-precision, but the cost is marginal and
      // performance is not the purpose of double-precision computations.  As with the CPU code,
      // all of the numbers are already scaled by the position fixed-precision scaling factor.
      const TCALC inv_tile_pts = (TCALC)(1.0) /
                                 (sh_tile_tpts[absc_import_idx] + sh_tile_tpts[ordi_import_idx]);
      const TCALC tx_cog = (sh_tile_xcog[absc_import_idx] + sh_tile_xcog[ordi_import_idx]) *
                           inv_tile_pts;
      const TCALC ty_cog = (sh_tile_ycog[absc_import_idx] + sh_tile_ycog[ordi_import_idx]) *
                           inv_tile_pts;
      const TCALC tz_cog = (sh_tile_zcog[absc_import_idx] + sh_tile_zcog[ordi_import_idx]) *
                           inv_tile_pts;
      int read_idx  = ((warp_lane_idx <  tile_length) * local_absc_start) +
                      ((warp_lane_idx >= tile_length) * local_ordi_start) + tile_lane_idx +
                      EXCL_GMEM_OFFSET;
#ifdef TCALC_IS_SINGLE
      const llint x_center = LLCONV_FUNC(tx_cog);
      const llint y_center = LLCONV_FUNC(ty_cog);
      const llint z_center = LLCONV_FUNC(tz_cog);
      const TCALC t_xcrd   = (TCALC)(gmem_r.xcrd[read_idx] - x_center) * poly_psw.inv_gpos_scale;
      const TCALC t_ycrd   = (TCALC)(gmem_r.ycrd[read_idx] - y_center) * poly_psw.inv_gpos_scale;
      const TCALC t_zcrd   = (TCALC)(gmem_r.zcrd[read_idx] - z_center) * poly_psw.inv_gpos_scale;
#else
      // Convert the negative values of the center-of-geometry coordinates so that the result
      // can feed into splitFPSum() (which adds A + B).
      const int95_t x_center = doubleToInt95(-tx_cog);
      const int95_t y_center = doubleToInt95(-ty_cog);
      const int95_t z_center = doubleToInt95(-tz_cog);
      const int95_t x_atom   = { gmem_r.xcrd[read_idx], gmem_r.xcrd_ovrf[read_idx] };
      const int95_t y_atom   = { gmem_r.ycrd[read_idx], gmem_r.ycrd_ovrf[read_idx] };
      const int95_t z_atom   = { gmem_r.zcrd[read_idx], gmem_r.zcrd_ovrf[read_idx] };
      const int95_t x95_tmp  = splitFPSum(x_atom, x_center);
      const int95_t y95_tmp  = splitFPSum(y_atom, y_center);
      const int95_t z95_tmp  = splitFPSum(z_atom, z_center);
      const TCALC t_xcrd = splitFPToReal(x95_tmp) * poly_psw.inv_gpos_scale;
      const TCALC t_ycrd = splitFPToReal(y95_tmp) * poly_psw.inv_gpos_scale;
      const TCALC t_zcrd = splitFPToReal(z95_tmp) * poly_psw.inv_gpos_scale;
#endif
      const int t_ljidx   = gmem_r.lj_idx[read_idx];
      const TCALC t_q     = gmem_r.charges[read_idx];
      const int nljt      = sh_n_lj_types[absc_import_idx];
      const int lj_offset = sh_ljabc_offsets[absc_import_idx];
#ifdef COMPUTE_ENERGY
      TCALC elec_nrg = 0.0;
      TCALC vdw_nrg = 0.0;
#endif
#ifdef COMPUTE_FORCE
      TCALC t_xfrc = (TCALC)(0.0);
      TCALC t_yfrc = (TCALC)(0.0);
      TCALC t_zfrc = (TCALC)(0.0);
#endif
      for (int i = 0; i < half_tile_length; i++) {
        int crd_src_lane = ((warp_lane_idx <  tile_length) * (warp_lane_idx + tile_length - i)) +
                           ((warp_lane_idx >= tile_length) *
                           (warp_lane_idx - three_halves_tile_length + i));
        crd_src_lane += tile_length * ((crd_src_lane <  tile_length &&
                                        warp_lane_idx < tile_length) +
                                       (crd_src_lane <             0 &&
                                        warp_lane_idx >= tile_length));
        const TCALC o_xcrd  = SHFL(t_xcrd,  crd_src_lane);
        const TCALC o_ycrd  = SHFL(t_ycrd,  crd_src_lane);
        const TCALC o_zcrd  = SHFL(t_zcrd,  crd_src_lane);
        const TCALC o_ljidx = SHFL(t_ljidx, crd_src_lane);
        const TCALC o_q     = SHFL(t_q,     crd_src_lane);
        const TCALC ot_qq   = t_q * o_q;
        const TCALC dx      = o_xcrd - t_xcrd;
        const TCALC dy      = o_ycrd - t_ycrd;
        const TCALC dz      = o_zcrd - t_zcrd;
        const TCALC r2      = (dx * dx) + (dy * dy) + (dz * dz);
#ifdef COMPUTE_FORCE
        TCALC fmag;
#endif
        if ((t_mask >> (crd_src_lane - (tile_length * (warp_lane_idx < tile_length)))) & 0x1) {
#ifdef COMPUTE_FORCE
          fmag = (TCALC)(0.0);
#endif
        }
        else {
          const TCALC invr      = rsqrt(r2);
          const TCALC invr2     = invr * invr;
          const TCALC invr4     = invr2 * invr2;
          const size_t ot_ljidx = (t_ljidx * nljt) + o_ljidx + lj_offset;
          const TCALC lja       = poly_nbk.lja_coeff[ot_ljidx];
          const TCALC ljb       = poly_nbk.ljb_coeff[ot_ljidx];
#ifdef COMPUTE_FORCE
          fmag = ((((TCALC)(6.0) * ljb) - ((TCALC)(12.0) * lja * invr4 * invr2)) * invr4 * invr4) -
                 (ot_qq * invr2 * invr);
#endif
#ifdef COMPUTE_ENERGY
          elec_nrg += ot_qq * invr;
          vdw_nrg  += ((lja * invr4 * invr2) - ljb) * invr4 * invr2;
#endif
        }
#ifdef COMPUTE_FORCE
        const TCALC fmag_dx = fmag * dx;
        const TCALC fmag_dy = fmag * dy;
        const TCALC fmag_dz = fmag * dz;
        t_xfrc += fmag_dx;
        t_yfrc += fmag_dy;
        t_zfrc += fmag_dz;
        int frc_ret_lane = ((warp_lane_idx <  tile_length) *
                            (warp_lane_idx + three_halves_tile_length - i)) +
                           ((warp_lane_idx >= tile_length) * (warp_lane_idx - tile_length + i));
        frc_ret_lane += tile_length *
                        ((frc_ret_lane >= tile_length   && warp_lane_idx >= tile_length) -
                         (frc_ret_lane >= warp_size_int && warp_lane_idx <  tile_length));
        t_xfrc -= SHFL(fmag_dx, frc_ret_lane);
        t_yfrc -= SHFL(fmag_dy, frc_ret_lane);
        t_zfrc -= SHFL(fmag_dz, frc_ret_lane);
#endif
      }
#ifdef COMPUTE_ENERGY
      llint elec_acc = LLCONV_FUNC(elec_nrg * scw.nrg_scale_f);
      WARP_REDUCE_DOWN(elec_acc);
      llint vdw_acc  = LLCONV_FUNC(vdw_nrg * scw.nrg_scale_f);
      WARP_REDUCE_DOWN(vdw_acc);
      if (warp_lane_idx == 0) {
        atomicAdd((ullint*)&sh_elec_acc[absc_import_idx], (ullint)(elec_acc));
        atomicAdd((ullint*)&sh_vdw_acc[absc_import_idx], (ullint)(vdw_acc));
      }
#endif
#ifdef COMPUTE_FORCE
      read_idx -= EXCL_GMEM_OFFSET;
#  ifdef SPLIT_FORCE_ACCUMULATION
      atomicSplit(t_xfrc * poly_psw.frc_scale_f, read_idx, sh_xfrc, sh_xfrc_overflow);
      atomicSplit(t_yfrc * poly_psw.frc_scale_f, read_idx, sh_yfrc, sh_yfrc_overflow);
      atomicSplit(t_zfrc * poly_psw.frc_scale_f, read_idx, sh_zfrc, sh_zfrc_overflow);
#  else
      atomicAdd((ullint*)&sh_xfrc[read_idx], (ullint)(LLCONV_FUNC(t_xfrc * poly_psw.frc_scale_f)));
      atomicAdd((ullint*)&sh_yfrc[read_idx], (ullint)(LLCONV_FUNC(t_yfrc * poly_psw.frc_scale_f)));
      atomicAdd((ullint*)&sh_zfrc[read_idx], (ullint)(LLCONV_FUNC(t_zfrc * poly_psw.frc_scale_f)));
#  endif
#endif

      // Increment the tile counter.  No synchronization is needed here as each thread will take
      // a deterministic route through the next iteration of this loop, or exit.  The design
      // decision to handle all tiles in the same manner, irrespective of their excluded content,
      // makes this asynchronous method the simplest and most efficient.
      pos += warps_per_block;
    }
    __syncthreads();

    // Re-read the __shared__ memory value as a local constant to avoid having to carry it through
    // the loops above and thus reduce register pressure.  Commit the locally accumulated forces
    // back to global arrays.
    pos = (tile_sides_per_warp * warp_idx) + tile_side_offset;
#ifdef COMPUTE_FORCE
#  ifdef TCALC_IS_SINGLE
#    ifdef SPLIT_FORCE_ACCUMULATION
    pos = accumulateTileProperty(pos, 0, nbwu_map, sh_xfrc, sh_xfrc_overflow, poly_psw.xfrc);
    pos = accumulateTileProperty(pos, 1, nbwu_map, sh_yfrc, sh_yfrc_overflow, poly_psw.yfrc);
    pos = accumulateTileProperty(pos, 2, nbwu_map, sh_zfrc, sh_zfrc_overflow, poly_psw.zfrc);
#    else
    pos = accumulateTileProperty(pos, 0, nbwu_map, sh_xfrc, poly_psw.xfrc);
    pos = accumulateTileProperty(pos, 1, nbwu_map, sh_yfrc, poly_psw.yfrc);
    pos = accumulateTileProperty(pos, 2, nbwu_map, sh_zfrc, poly_psw.zfrc);
#    endif
#  else
    pos = accumulateTileProperty(pos, 0, nbwu_map, sh_xfrc, sh_xfrc_overflow, poly_psw.xfrc,
                                 poly_psw.xfrc_ovrf);
    pos = accumulateTileProperty(pos, 1, nbwu_map, sh_yfrc, sh_yfrc_overflow, poly_psw.yfrc,
                                 poly_psw.yfrc_ovrf);
    pos = accumulateTileProperty(pos, 2, nbwu_map, sh_zfrc, sh_zfrc_overflow, poly_psw.zfrc,
                                 poly_psw.zfrc_ovrf);
#  endif
#endif
#ifdef COMPUTE_ENERGY
    pos = threadIdx.x;
    const int import_count_ii = nbwu_map[0];
    const int padded_import_count_ii = devcRoundUp(import_count_ii, warp_size_int);
    while (pos < padded_import_count_ii) {
      if (pos < import_count_ii) {
        const int write_idx = (sh_system_indices[pos] * scw.data_stride) +
                              (int)(StateVariable::ELECTROSTATIC);
        atomicAdd((ullint*)&scw.instantaneous_accumulators[write_idx], (ullint)(sh_elec_acc[pos]));
      }
      pos += blockDim.x;
    }
    while (pos < 2 * padded_import_count_ii) {
      const int rel_pos = pos - padded_import_count_ii;
      if (rel_pos < import_count_ii) {
        const int write_idx = (sh_system_indices[rel_pos] * scw.data_stride) +
                              (int)(StateVariable::VDW);
        atomicAdd((ullint*)&scw.instantaneous_accumulators[write_idx],
                  (ullint)(sh_vdw_acc[rel_pos]));
      }
      pos += blockDim.x;
    }
    while (pos < 3 * padded_import_count_ii) {
      const int rel_pos = pos - (2 * padded_import_count_ii);
      if (rel_pos < import_count_ii) {
        const int write_idx = (sh_system_indices[rel_pos] * scw.data_stride) +
                              (int)(StateVariable::GENERALIZED_BORN);
        atomicAdd((ullint*)&scw.instantaneous_accumulators[write_idx],
                  (ullint)(sh_gb_acc[rel_pos]));
      }
      pos += blockDim.x;
    }
#endif
    
    // Increment the work unit counter
    __syncthreads();
    if (threadIdx.x == 0) {
      const size_t prog_counter_idx = (ctrl.step & twice_warp_bits_mask_int);
      nbwu_idx = atomicAdd(&ctrl.nbwu_progress[prog_counter_idx], 1);
    }
    __syncthreads();
  }

  // Set the block counters for future iterations of this kernel
  if (blockIdx.x == 0 && threadIdx.x < warp_size_int) {
    const int step_modulus = (ctrl.step & twice_warp_bits_mask_int);
    if (step_modulus == 0) {
      ctrl.nbwu_progress[threadIdx.x + warp_size_int] = gridDim.x;
    }
    if (step_modulus == warp_size_int) {
      ctrl.nbwu_progress[threadIdx.x] = gridDim.x;
    }
  }
}

#undef EXCL_GMEM_OFFSET
